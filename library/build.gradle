plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace 'ir.mahdiparastesh.mcdtp'
    compileSdk Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion '34.0.0'

    defaultConfig {
        minSdk Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdk Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(project.VERSION_CODE)
        versionName project.VERSION_NAME
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'com.google.android.material:material:1.9.0'
}

afterEvaluate { // You only need to set the access token and execute "gradle publish"...
    publishing {
        repositories {
            maven {
                name 'GitHubPackages'
                url 'https://maven.pkg.github.com/fulcrum6378/mcdtp'
                credentials {
                    username 'fulcrum6378'
                    password System.getenv('GHP_TOKEN')
                }
            }
            /*maven {
                name 'OSSRH'
                url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username 'fulcrum1378'
                    password System.getenv('JiraPassword')
                }
            }*/
        }
        publications {
            release(MavenPublication) {
                from components.release
                groupId 'ir.mahdiparastesh'
                artifactId 'mcdtp'
                version project.VERSION_NAME
            }
        }
    }
}
